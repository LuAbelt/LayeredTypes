-- Use Case 2: Machine Learning Pipelines
-- For simplicity, we limit ourselves to the following operations:
-- createDataset(c,r) -- create a dataset with r rows and c columns
-- add_rows(d,n) -- add n rows to dataset d
-- add_cols(d,n) -- add n columns to dataset d
-- reduce_cols(d,c) -- reduce the number of columns to c
-- sample(d,n) -- sample n rows from dataset d
-- train(d) -- train a model on dataset d

-- Get Dimensions of a Data Set
n_rows :: liquid :: { d:DataSet | true } -> { r:Int | (r >= 0) && (r == n_rows(d)) }
n_cols :: liquid :: { d:DataSet | true } -> { c:Int | (c >= 0) && (c == n_cols(d)) }

-- Add rows or columns to a data set
add_cols :: liquid :: { d:DataSet | true } -> {n:Int | n>0} -> { d2:DataSet | (n_cols(d2) == (n_cols(d) + n)) && (n_rows(d2) == n_rows(d)) }
add_rows :: liquid :: { d:DataSet | n_cols(d) > 0 } -> {n:Int | n>0} -> { d2:DataSet | (n_rows(d2) == (n_rows(d) + n)) && (n_cols(d2) == n_cols(d)) }

-- Perform operations that depend on the number of columns
reduce_cols :: liquid :: { d:DataSet | true } -> { c:Int | (c >= 0) && (c < (n_cols(d))) } -> { d2:DataSet | (n_cols(d2) == c) && (n_rows(d2) == n_rows(d)) }

-- Perform operations that depend on the number of rows
sample :: liquid :: { d:DataSet | true } -> { n:Int | (n >= 0) && (n <= (n_rows(d))) } -> { d2:DataSet | (n_rows(d2) == n) && (n_cols(d2) == n_cols(d)) }

-- Operations that depend on both the number of rows and columns
-- When training a model, we should have at least 10 rows per column
train :: liquid :: { d:DataSet | (n_rows(d) >= 10 * (n_cols(d))) } -> { m:Model | true }

create_dataset :: liquid :: { c:Int | (c >= 0) } -> { r:Int | (r >= 0) } -> { d:DataSet | (n_cols(d) == c) && (n_rows(d) == r) }

-- Scenario 1: Operations with only rows

empty_dataset :: liquid :: { d:DataSet | true }

let empty_dataset := create_dataset(10,0) in {
    dataset :: liquid :: { d:DataSet | true }
    let dataset := add_rows(empty_dataset, 50) in {
        -- This fails because we do not have enough rows
        s :: liquid :: { d:DataSet | true }
        let s := sample(dataset, 100) in {
            s
        }

        let s := sample(dataset, 10) in {
            s
        }
    }
}

-- Scenario 2: Operations with only columns

let empty_dataset := create_dataset(10,0) in {
    dataset :: liquid :: { d:DataSet | true }
    let dataset := add_cols(empty_dataset, 50) in {
        c :: liquid :: { d:DataSet | true }
        let c := reduce_cols(dataset, 5) in {
            c
        }

        let c := reduce_cols(dataset, 100) in {
            c
        }
    }
}

-- Scenario 3: Operations with rows and columns
-- We show that calling train on this data set fails because we do not have enough rows

let empty_dataset := create_dataset(10,0) in {
    dataset :: liquid :: { d:DataSet | true }
    let dataset := add_rows(empty_dataset, 50) in {
       train(dataset)

       small_dataset :: liquid :: { d:DataSet | true }
       let small_dataset := reduce_cols(dataset, 5) in {
           train(small_dataset)
       }
    }
}

